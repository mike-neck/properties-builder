apply plugin : 'groovy'
apply plugin : 'idea'

group = 'org.mikeneck.groovy'
version = '0.5'
archivesBaseName = 'properties-builder'

def jdkVersion = '1.7'
sourceCompatibility = jdkVersion
targetCompatibility = jdkVersion

repositories {
    mavenCentral ()
}

dependencies {
    groovy 'org.codehaus.groovy:groovy-all:2.1.3'
    testCompile ('junit:junit-dep:4.11') {
        exclude module : 'hamcrest'
        exclude module : 'hamcrest-core'
    }
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile ('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude module : 'groovy-all'
        exclude module : 'hamcrest-core'
        exclude module : 'junit-dep'
    }
}

project.tasks.ideaModule.doFirst {
    ['main', 'test'].each {phase ->
        ["src/$phase/groovy/${project.group.replace('.', '/')}", "src/$phase/resources"].each {path ->
            if(!file(path).exists()) {
                println "making directory : $path"
                mkdir(path)
            }
        }
    }
}

tasks.withType(Compile) {
    options.encoding = 'UTF-8'
}

idea.project.ipr {
    withXml { provider ->
        def map = provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping
        map.@vcs = 'Git'
        map.@directory = project.properties.projectDir

    }
}
